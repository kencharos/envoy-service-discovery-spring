buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.8'
    }
}
plugins {
    id 'org.springframework.boot' version '2.1.3.RELEASE'
    id 'java'
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.google.protobuf'
apply plugin: "idea"

group = 'my.sdtest'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'


def reactiveGrpcVersion = '0.9.0'
def grpcVersion = '1.18.0'
def protobufVersion = '3.7.0'

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:Greenwich.RELEASE"
    }
}
dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    compile "com.google.protobuf:protobuf-java:${protobufVersion}"
    compile "io.grpc:grpc-stub:${grpcVersion}"
    compile "io.grpc:grpc-protobuf:${grpcVersion}"
    compile "com.salesforce.servicelibs:reactor-grpc-stub:${reactiveGrpcVersion}"
    compile 'io.github.lognet:grpc-spring-boot-starter:3.1.0'

    // distribute tracing deps
    compile "org.springframework.cloud:spring-cloud-starter-zipkin"
    compile group: 'io.zipkin.brave', name: 'brave-instrumentation-grpc', version: '5.6.3'

    // consul, config
    compile "org.springframework.cloud:spring-cloud-starter-consul-discovery"



    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
}


task cleanProtoGen{
    doFirst{
        delete("$projectDir/src/main/generated-proto")
    }
}
clean.dependsOn cleanProtoGen


sourceSets {
    main {
        proto {
            // default 'src/main/proto'
            srcDir '../api/proto'
            include '**/*.proto' // select target proto file
        }
    }
}

protobuf {
    protoc {
        // The artifact spec for the Protobuf Compiler
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
        reactor {
            artifact = "com.salesforce.servicelibs:reactor-grpc:${reactiveGrpcVersion}:jdk8@jar"
        }
    }
    generateProtoTasks {

        all().each { task ->
            task.builtins {
                java {
                    outputSubDir = 'generated-proto'
                }
            }
            task.plugins {
                grpc {
                    outputSubDir = 'generated-proto'
                }
            }
            task.plugins {
                reactor {
                    outputSubDir = 'generated-proto'
                }
            }
        }
    }
    generatedFilesBaseDir = "${projectDir}/src/"
}
